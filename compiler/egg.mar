| Expression-Guided Graph  
| A representation of code that is simplified and more suitable for
| type-checking.
|
| Types:
|   Names of types are resolved. Usages of type definitions (which are more like
|   aliases anyway) have been resolved so that types have no names anymore –
|   they are completely structural.
|
| Functions:
|   Names of locals and types are resolved. Function calls are not resolved to
|   specific functions yet, as this requires overloading resolution. Nested
|   expressions in function bodies have been flattened to a series of small
|   expressions.

import breakfast.mar

struct Egg { funs: List[EggFun] }

struct EggFun {
  name: String, params: Slice[BreakfastType], return_type: BreakfastType,
  impl: EggFunImpl
}
enum EggFunImpl { code: EggCode, builtin: Builtin }
struct EggCode { exprs: Slice[EggExpr], body: EggBody }
struct EggId { int: Int }
struct EggBody { params: Slice[EggId], exprs: Slice[EggId], returns: EggId }

enum EggExpr {
  param,
  int: Int,
  string: String,
  symbol: String,
  switch_: EggSwitch,
  tag: EggTag,
  call: EggCall,
}
struct EggSwitch { condition: EggId, cases: Map[String, EggBody] }
struct EggTag { symbol: String, args: Slice[EggId] }
struct EggCall { receiver: String, args: Slice[EggId] }

fun ==(a: EggId, b: EggId): Bool { a.int == b.int }
fun hash(hasher: &Hasher, id: EggId) { hasher.hash(id.int) }

fun get(code: EggCode, id: EggId): EggExpr { code.exprs.get(id.int) }

fun write[W](writer: W, egg: Egg) {
  var first = true
  for fun_ in egg.funs do {
    if first then first = false else writer."\n"
    writer."{fun_.name}"
    for param in fun_.params do writer." {param}"
    writer." -> {fun_.return_type} ="
    switch fun_.impl
    case code(code) writer.write(code.body, code, 1)
    case builtin(builtin) writer." ..."
  }
}
fun write[W](writer: W, id: EggId) { writer."@{id.int}" }
fun write[W](writer: W, body: EggBody, code: EggCode, indentation: Int) {
  for param in body.params do writer." {param}"
  writer."\n"
  for id in body.exprs do {
    for i in 0..indentation do writer."  "
    writer."{id} = "
    writer.write(code.get(id), code, indentation)
    writer."\n"
  }
  for i in 0..indentation do writer."  "
  writer."{body.returns}"
}
fun write[W](writer: W, expr: EggExpr, code: EggCode, indentation: Int) {
  switch expr
  case int(int) writer."int {int}"
  case string(string) writer."string {string.debug()}"
  case symbol(symbol) writer."symbol {symbol}"
  case param(param) writer."param {param}"
  case switch_(switch_) {
    writer."switch {switch_.condition}"
    for case_ in switch_.cases do {
      writer."\n"
      for i in 0..indentation do writer."  "
      writer."  {case_.key}:"
      writer.write(case_.value, code, indentation + 2)
    }
  }
  case call(call) {
    writer."call {call.receiver}"
    for arg in call.args do writer." {arg}"
  }
  case tag(tag) {
    writer."tag {tag.symbol}"
    for arg in tag.args do writer." {arg}"
  }
}


| Type Algorithms  
| Turns out, some of the things we want to do with types are not trivial because
| of recursive types. For example, joining these two enums results in a mess:
|
| | Foo (| Bar ^2) = | Foo (| Bar (| Foo (| Bar (...))))
| | Foo ^1         = | Foo (| Foo (| Foo (| Foo (...))))
|
| The joined type is this:
|
| | Foo (| Foo (| Foo ^1) Bar (| Foo (| Bar ^2)))
|
| Basically, the top-level is always the Foo variant and the level below that
| decides whether it is Foo forever or whether Foo and Bar alternate.


| Variable Scoping  
| Tracks which names are in scope using a simple vector:
| - When new names are defined, appends items to the vector.
| - When names are referenced, walks the vector from the back, searching for the
|   name. Names that are defined later shadow previously defined names.
| - When bodies are compiled, save the length of the vector before entering the
|   body. When exiting the body, trim the vector to that length – all names
|   defined inside the body automatically go out of scope.

struct Visible { vars: List[NameBinding] }
struct NameBinding { name: String, id: EggId }
fun visible(): _ { Visible { vars = list[NameBinding]() } }
fun define(visible: &Visible, name: String, id: EggId) {
  visible.vars.&.push(NameBinding { name, id })
}
fun lookup(visible: Visible, name: String): Maybe[EggId] {
  for binding in visible.vars.to_slice().rev_iter() do
    if binding.name == name then return some(binding.id)
  none[EggId]()
}
struct VisibleSnapshot { vars_len: Int }
fun snapshot(visible: Visible): _ {
  VisibleSnapshot { vars_len = visible.vars.len }
}
fun restore(visible: &Visible, snapshot: VisibleSnapshot) {
  visible.vars.len = snapshot.vars_len
}

| Converting Toast To Egg  

fun to_egg(toast: Toast): Egg {
  var funs = list[EggFun]()

  for builtin in builtins do funs.&.push(builtin.create_builtin_fun())
  for def in toast.defs do
    if def is fun_(fun_) then
      funs.&.push(EggFun {
        name = fun_.name,
        params = {
          var params = list[BreakfastType]()
          for param in fun_.params do
            params.&.push(param.type.to_egg(toast))
          params.to_slice()
        },
        return_type = fun_.return_type.to_egg(toast),
        impl = EggFunImpl.code({
          var visible = visible()
          var compiler = egg_code_compiler().&
          var body = compiler.egg_body_compiler().&

          for param in fun_.params do
            visible.&.define(param.name, body.push_param())
          var returned = fun_.body.to_egg(body, visible.&)
          var body = body.finish(returned)

          EggCode {
            exprs = compiler.exprs.to_slice(),
            body,
          }
        })
      })

  Egg { funs }
}

fun to_egg(type: ToastType, toast: Toast): BreakfastType {
  type.to_egg(toast, map[String, BreakfastType](), list[OngoingTypeCall](), 0)
}
struct OngoingTypeCall { depth: Int, name: String, args: Slice[BreakfastType] }
fun to_egg(
  type: ToastType, toast: Toast, env: Map[String, BreakfastType],
  parent_calls: List[OngoingTypeCall], depth: Int,
): BreakfastType {
  switch type
  case variable(variable)
    if env.get_maybe(variable) is some(type)
    then type
    else BreakfastType.primitive(variable)
  case call(call) {
    var name = call.name
    
    | Lower the arguments.
    var args = list[BreakfastType]()
    for arg in call.args do
      args.&.push(arg.to_egg(toast, env, parent_calls, depth))
    var args = args.to_slice()

    | Handle builtin types.
    if name == "Int" then {
      call.args.is_empty() or panic("called Int with args")
      return type_int
    }
    if name == "String" then {
      call.args.is_empty() or panic("called String with args")
      return type_string
    }
    if name == "Type" then {
      call.args.is_empty() or panic("called Type with args")
      return type_type
    }

    | Are we recursively lowering a type? If so, we need to insert a recursive
    | type instead of endlessly digging down into the type.
    for call in parent_calls do
      if call.name == name and call.args == args then
        return BreakfastType.recursive(depth - call.depth)

    | Look for a user-defined type with the name.
    for def in toast.defs do
      if def is type(def) then
        if def.name == name then {
          var inner_env = map[String, BreakfastType]()
          def.params.len == args.len or
            panic("tried to instantiate {def.name} with {args.len} args, but it needs {def.params.len}")
          for both in zip(def.params.iter(), args.iter()) do
            inner_env.&.put(both.a, both.b)
          parent_calls.&.push(OngoingTypeCall { name, args, depth })
          var type = def.type.to_egg(toast, inner_env, parent_calls, depth)
          parent_calls.&.pop()
          return type
        }
    panic("{name} doesn't exist")
  }
  case enum_(enum_) {
    var variants = map[String, Slice[BreakfastType]]()
    for variant in enum_ do {
      var args = list[BreakfastType]()
      for arg in variant.value do
        args.&.push(arg.to_egg(toast, env, parent_calls, depth + 1))
      variants.&.put(variant.key, args.to_slice())
    }
    BreakfastType.enum_(variants)
  }
}

fun create_builtin_fun(builtin: Builtin): EggFun {
  var signature = builtin.signature()
  EggFun {
    name = "builtin_{builtin}",
    params = signature.params,
    return_type = signature.return_type,
    impl = EggFunImpl.builtin(builtin),
  }
}

struct EggCodeCompiler { exprs: List[EggExpr] }
struct EggBodyCompiler {
  parent: &EggCodeCompiler, params: List[EggId], exprs: List[EggId]
}
fun egg_code_compiler(): _ { EggCodeCompiler { exprs = list[EggExpr]() } }
fun egg_body_compiler(parent: &EggCodeCompiler): _ {
  EggBodyCompiler { parent, params = list[EggId](), exprs = list[EggId]() }
}
fun finish(body: EggBodyCompiler, id: EggId): EggBody {
  EggBody {
    params = body.params.to_slice(), exprs = body.exprs.to_slice(), returns = id
  }
}
fun push_param(body: &EggBodyCompiler): EggId {
  var id = EggId { int = body.parent.exprs.len }
  body.parent.exprs.&.push(EggExpr.param)
  body.params.&.push(id)
  id
}
fun push(body: &EggBodyCompiler, expr: EggExpr): EggId {
  var id = EggId { int = body.parent.exprs.len }
  body.parent.exprs.&.push(expr)
  body.exprs.&.push(id)
  id
}

fun to_egg(toast: ToastExpr, body: &EggBodyCompiler, visible: &Visible): EggId {
  switch toast
  case int(int) body.push(EggExpr.int(int))
  case string(str) body.push(EggExpr.string(str))
  case symbol(symbol) body.push(EggExpr.symbol(symbol))
  case name(name)
    visible.lookup(name) or panic("unknown referenced name {name}")
  case switch_(switch_) {
    var condition = switch_.condition.to_egg(body, visible)
    var cases = map[String, EggBody]()
    for case_ in switch_.cases do {
      var snapshot = visible.snapshot()
      var case_body = body.parent.egg_body_compiler().&
      for param in case_.params do
        visible.define(param, case_body.push_param())
      var returns = case_.body.to_egg(case_body, visible)
      var case_body = case_body.finish(returns)
      visible.restore(snapshot)
      cases.&.put(case_.symbol, case_body)
    }
    body.push(EggExpr.switch_(EggSwitch { condition, cases }))
  }
  case call(call) {
    var args = list[EggId]()
    for arg in call.args do args.&.push(arg.to_egg(body, visible))
    var args = args.to_slice()

    switch call.receiver.*
    case name(receiver) body.push(EggExpr.call(EggCall { receiver, args }))
    case symbol(symbol) body.push(EggExpr.tag(EggTag { symbol, args }))
    default panic("called non-name {call.receiver.debug()}")
  }
  case var_(var_) {
    var value = var_.value.to_egg(body, visible)
    visible.define(var_.name, value)
    value
  }
  case body(exprs) {
    var last = none[EggId]()
    for expr in exprs do last = some(expr.to_egg(body, visible))
    last or panic("empty body")
  }
}
