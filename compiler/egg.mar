| Expression-Guided Graph  

import breakfast.mar

struct Egg { defs: Map[String, EggDef] }
enum EggDef { value: EggFun, fun_: EggFun, builtin: Builtin }
struct EggFun { exprs: Slice[EggExpr], body: EggBody }
struct EggId { int: Int }

struct EggBody { params: Slice[EggId], exprs: Slice[EggId], returns: EggId }
enum EggExpr {
  param,
  int: Int,
  string: String,
  symbol: String,
  global: String,
  switch_: EggSwitch,
  call: EggCall,
}
struct EggSwitch { condition: EggId, cases: Map[String, EggBody] }
struct EggCall { receiver: EggId, args: Slice[EggId] }

fun ==(a: EggId, b: EggId): Bool { a.int == b.int }
fun hash(hasher: &Hasher, id: EggId) { hasher.hash(id.int) }

fun get(fun_: EggFun, id: EggId): EggExpr { fun_.exprs.get(id.int) }

fun write[W](writer: W, egg: Egg) {
  var first = true
  for def in egg.defs do {
    if first then first = false else writer."\n"
    writer."{def.key}:"
    switch def.value
    case value(fun_) writer.write(fun_.body, fun_, 1)
    case fun_(fun_) writer.write(fun_.body, fun_, 1)
    case builtin(builtin) writer." -"
  }
}
fun write[W](writer: W, id: EggId) { writer.":{id.int}" }
fun write[W](writer: W, body: EggBody, fun_: EggFun, indentation: Int) {
  for param in body.params do writer." {param}"
  writer."\n"
  for id in body.exprs do {
    for i in 0..indentation do writer."  "
    writer."{id} = "
    writer.write(fun_.get(id), fun_, indentation)
    writer."\n"
  }
  for i in 0..indentation do writer."  "
  writer."{body.returns}"
}
fun write[W](writer: W, expr: EggExpr, fun_: EggFun, indentation: Int) {
  switch expr
  case int(int) writer."int {int}"
  case string(string) writer."string {string.debug()}"
  case symbol(symbol) writer."symbol {symbol}"
  case param(param) writer."param {param}"
  case global(global) writer."global {global}"
  case switch_(switch_) {
    writer."switch {switch_.condition}"
    for case_ in switch_.cases do {
      writer."\n"
      for i in 0..indentation do writer."  "
      writer."  {case_.key}:"
      writer.write(case_.value, fun_, indentation + 2)
    }
  }
  case call(call) {
    writer."call {call.receiver}"
    for arg in call.args do writer." {arg}"
  }
}

| Variable Scoping  
| Tracks which names are in scope using a simple vector:
| - When new names are defined, appends items to the vector.
| - When names are referenced, walks the vector from the back, searching for the
|   name. Names that are defined later shadow previously defined names.
| - When bodies are compiled, save the length of the vector before entering the
|   body. When exiting the body, trim the vector to that length â€“ all names
|   defined inside the body automatically go out of scope.

struct Visible { vars: List[Tuple2[String, NameBinding]] }
enum NameBinding { global: String, local: EggId }
fun visible(): _ { Visible { vars = list[Tuple2[String, NameBinding]]() } }
fun define(visible: &Visible, name: String, value: NameBinding) {
  visible.vars.&.push(tuple(name, value))
}
fun lookup(visible: Visible, name: String): Maybe[NameBinding] {
  for binding in visible.vars.to_slice().rev_iter() do
    if binding.a == name then return some(binding.b)
  none[NameBinding]()
}
struct VisibleSnapshot { vars_len: Int }
fun snapshot(visible: Visible): _ {
  VisibleSnapshot { vars_len = visible.vars.len }
}
fun restore(visible: &Visible, snapshot: VisibleSnapshot) {
  visible.vars.len = snapshot.vars_len
}

| Converting Toast To Egg  

fun to_egg(toast: Toast): Egg {
  var defs = map[String, EggDef]()
  var visible = visible()

  var body = toast.body or panic("Top-level Toast is not a body.")

  for builtin in builtins do {
    var name = "builtin_{builtin}"
    visible.&.define(name, NameBinding.global("@{name}"))
    defs.&.put("@{name}", EggDef.builtin(builtin))
  }
  for toast in body do {
    var def = toast.def or panic("top-level non-def:\n{toast}")
    visible.&.define(def.name, NameBinding.global(def.name))
  }
  for toast in body do {
    var def = toast.def.unwrap()
    var name = def.name
    var value = def.value.*
    var snapshot = visible.snapshot()
    var value =
      switch value
      case fun_(fun_) {
        var compiler = egg_fun_compiler().&
        var body = compiler.egg_body_compiler().&
        for param in fun_.params do
          visible.&.define(param, NameBinding.local(body.push_param()))
        var body = body.finish(fun_.body.to_egg(body, visible.&))
        EggDef.fun_(EggFun {
          exprs = compiler.exprs.to_slice(), body
        })
      }
      default {
        var compiler = egg_fun_compiler().&
        var body = compiler.egg_body_compiler().&
        var body = body.finish(value.to_egg(body, visible.&))
        EggDef.value(EggFun {
          exprs = compiler.exprs.to_slice(), body
        })
      }
    visible.&.restore(snapshot)
    defs.&.put(name, value)
  }

  Egg { defs }
}

struct EggFunCompiler { exprs: List[EggExpr] }
struct EggBodyCompiler {
  parent: &EggFunCompiler, params: List[EggId], exprs: List[EggId]
}
fun egg_fun_compiler(): _ { EggFunCompiler { exprs = list[EggExpr]() } }
fun egg_body_compiler(parent: &EggFunCompiler): _ {
  EggBodyCompiler { parent, params = list[EggId](), exprs = list[EggId]() }
}
fun finish(body: EggBodyCompiler, id: EggId): EggBody {
  EggBody {
    params = body.params.to_slice(), exprs = body.exprs.to_slice(), returns = id
  }
}
fun push_param(body: &EggBodyCompiler): EggId {
  var id = EggId { int = body.parent.exprs.len }
  body.parent.exprs.&.push(EggExpr.param)
  body.params.&.push(id)
  id
}
fun push(body: &EggBodyCompiler, expr: EggExpr): EggId {
  var id = EggId { int = body.parent.exprs.len }
  body.parent.exprs.&.push(expr)
  body.exprs.&.push(id)
  id
}

fun to_egg(toast: Toast, body: &EggBodyCompiler, visible: &Visible): EggId {
  switch toast
  case int(int) body.push(EggExpr.int(int))
  case string(str) body.push(EggExpr.string(str))
  case symbol(symbol) body.push(EggExpr.symbol(symbol))
  case name(name) {
    switch visible.lookup(name) or panic("unknown referenced name {name}")
    case global(global) body.push(EggExpr.global(global))
    case local(local) local
  }
  case switch_(switch_) {
    var condition = switch_.condition.to_egg(body, visible)
    var cases = map[String, EggBody]()
    for case_ in switch_.cases do {
      var snapshot = visible.snapshot()
      var case_body = body.parent.egg_body_compiler().&
      for param in case_.params do
        visible.define(param, NameBinding.local(case_body.push_param()))
      var returns = case_.body.to_egg(case_body, visible)
      var case_body = case_body.finish(returns)
      visible.restore(snapshot)
      cases.&.put(case_.symbol, case_body)
    }
    body.push(EggExpr.switch_(EggSwitch { condition, cases }))
  }
  case call(call) {
    var receiver = call.receiver.to_egg(body, visible)
    var args = list[EggId]()
    for arg in call.args do args.&.push(arg.to_egg(body, visible))
    body.push(EggExpr.call(EggCall { receiver, args = args.to_slice() }))
  }
  case def(def) {
    var value = def.value.to_egg(body, visible)
    visible.define(def.name, NameBinding.local(value))
    value
  }
  case fun_(fun_) panic("non-top-level function")
  case body(exprs) {
    var last = none[EggId]()
    for expr in exprs do last = some(expr.to_egg(body, visible))
    last or panic("empty body")
  }
}
