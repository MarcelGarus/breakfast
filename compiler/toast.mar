| Totally Okay Abstract Syntax Tree  
| Tree that represents the syntactical language constructs. This data structure
| should roughly correspond to the content of Breakfast files and be suitable
| for formatting etc.

import breakfast.mar

struct Toast { defs: Slice[ToastDef] }
enum ToastDef { type: ToastTypeDef, fun_: ToastFun }

struct ToastTypeDef { name: String, params: Slice[String], type: ToastType }
enum ToastType {
  variable: String,
  call: ToastTypeCall,
  enum_: Map[String, Slice[ToastType]],
}
struct ToastTypeCall { name: String, args: Slice[ToastType] }

struct ToastFun {
  name: String,
  params: Slice[ToastParam],
  return_type: ToastType,
  body: ToastExpr,
}
struct ToastParam { name: String, type: ToastType }
enum ToastExpr {
  int: Int,
  string: String,
  symbol: String,
  name: String,
  switch_: ToastSwitch,
  call: ToastCall,
  var_: ToastVar,
  body: Slice[ToastExpr],
}
struct ToastSwitch { condition: &ToastExpr, cases: Slice[ToastCase] }
struct ToastCase { symbol: String, params: Slice[String], body: ToastExpr }
struct ToastCall { receiver: &ToastExpr, args: Slice[ToastExpr] }
struct ToastVar { name: String, value: &ToastExpr }

fun write[W](writer: W, toast: Toast) {
  var first = true
  for def in toast.defs do {
    if first then first = false else writer."\n"
    switch def
    case type(def) {
      writer."{def.name}"
      for param in def.params do writer." {param}"
      writer." = "
      writer."{def.type}"
    }
    case fun_(def) {
      writer."{def.name}"
      for param in def.params do writer." {param.name}: {param.type}"
      writer." -> {def.return_type}"
      writer." =\n"
      writer.write(def.body, 1)
    }
  }
}
fun write[W](writer: W, type: ToastType) {
  switch type
  case variable(variable) writer."{variable}"
  case call(call) {
    writer."({call.name}"
    for arg in call.args do writer." {arg}"
    writer.")"
  }
  case enum_(variants) {
    writer."|"
    for variant in variants do {
      writer." ({variant.key}"
      for arg in variant.value do writer." {arg}"
      writer.")"
    }
  }
}
fun write[W](writer: W, body: Slice[ToastExpr], indentation: Int) {
  var first = true
  for expr in body do {
    if first then first = false else writer."\n"
    writer.write(expr, indentation)
  }
}
fun write[W](writer: W, expr: ToastExpr, indentation: Int) {
  if not(expr is body) then
    for i in 0..indentation do writer."  "
  switch expr
  case int(int) writer."{int}"
  case string(str) writer."\"{str}\""
  case symbol(symbol) writer."{symbol}"
  case name(name) writer."{name}"
  case switch_(switch_) {
    writer."%\n"
    writer.write(switch_.condition.*, indentation + 1)
    for case_ in switch_.cases do {
      writer."\n"
      for i in 0..indentation do writer."  "
      writer."  {case_.symbol}:\n"
      writer.write(case_.body, indentation + 2)
    }
  }
  case call(call) {
    writer."call\n"
    writer.write(call.receiver.*, indentation + 1)
    writer."\n"
    writer.write(call.args, indentation + 1)
  }
  case var_(var_) {
    writer."var {var_.name} =\n"
    writer.write(var_.value.*, indentation + 1)
  }
  case body(body) writer.write(body, indentation)
}

| In this file, parsing refers to converting a single line of code into
| something toasty.

struct Parser { input: String, cursor: Int }
fun parser(input: String): _ { Parser { input, cursor = 0 } }
fun current(parser: Parser): Char {
  parser.input.chars().get_maybe(parser.cursor) or 0.lower_byte().to_char()
}
fun advance(parser: &Parser) { parser.cursor = parser.cursor + 1 }
fun is_at_end(parser: Parser): Bool { parser.cursor == parser.input.len }
fun consume_whitespace(parser: &Parser) {
  loop {
    parser.current().is_whitespace() or break
    parser.advance()
  }
}
fun is_done(parser: &Parser): Bool {
  parser.consume_whitespace()
  parser.is_at_end()
}
fun consume(parser: &Parser, char: Char): Bool {
  parser.consume_whitespace()
  parser.current() == char or return false
  parser.advance()
  true
}
fun consume(parser: &Parser, prefix: String): Bool {
  parser.consume_whitespace()
  var start = parser.cursor
  for char in prefix.chars() do {
    parser.current() == char or {
      parser.cursor = start
      return false
    }
    parser.advance()
  }
  true
}
fun parse_int(parser: &Parser): Maybe[Int] {
  parser.consume_whitespace()
  var num = 0
  var start = parser.cursor
  loop {
    var char = parser.current()
    char.is_digit() or break
    num = num * 10 + {char - #0}.to_int()
    parser.advance()
  }
  if start == parser.cursor then return none[Int]()
  some(num)
}
fun parse_string(parser: &Parser): Maybe[String] {
  parser.consume(#") or return none[String]()
  var start = parser.cursor
  loop {
    if parser.is_at_end() then panic("string doesn't end until end of input")
    if parser.current() == #" then break
    parser.advance()
  }
  var end = parser.cursor
  parser.advance() | consume the "
  some(parser.input.substr(start..end))
}
var identifier_chars = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz_+-*/<>"
fun parse_identifier(parser: &Parser): Maybe[String] {
  parser.consume_whitespace()
  var start = parser.cursor
  loop {
    identifier_chars.iter().&.contains(parser.current()) or break
    parser.advance()
  }
  var end = parser.cursor
  if start == end then return none[String]()
  var identifier = parser.input.substr(start..end)
  if identifier == "->" then {
    parser.cursor = start
    return none[String]()
  }
  some(identifier)
}
fun parse_name(parser: &Parser): Maybe[String] {
  parser.consume_whitespace()
  not(parser.current().is_upper()) or return none[String]()
  parser.parse_identifier()
}
fun parse_symbol(parser: &Parser): Maybe[String] {
  parser.consume_whitespace()
  parser.current().is_upper() or return none[String]()
  parser.parse_identifier()
}
fun parse_atom(parser: &Parser): Maybe[ToastExpr] {
  if parser.consume(#() then {
    var expr = parser.parse_expr() or panic("expected expr")
    parser.consume(#)) or panic("expected closing parenthesis")
    return some(expr)
  }
  if parser.parse_int() is some(int) then return some(ToastExpr.int(int))
  if parser.parse_string() is some(str) then return some(ToastExpr.string(str))
  if parser.parse_name() is some(name) then return some(ToastExpr.name(name))
  if parser.parse_symbol() is some(symbol) then return some(ToastExpr.symbol(symbol))
  none[ToastExpr]()
}
fun parse_expr(parser: &Parser): Maybe[ToastExpr] {
  var expr = parser.parse_atom() or return none[ToastExpr]()

  var args = list[ToastExpr]()
  loop args.&.push(parser.parse_atom() or break)
  if args.is_not_empty() then
    expr = ToastExpr.call(ToastCall {
      receiver = expr.put_on_heap(), args = args.to_slice()
    })

  if parser.consume(#=) then {
    var cursor_after_equals_sign = parser.cursor
    if parser.parse_expr() is some(value) then
      expr = ToastExpr.var_(create_var(expr, value))
    else
      parser.cursor = cursor_after_equals_sign - 1
  }

  some(expr)
}
fun parse_type_atom(parser: &Parser): Maybe[ToastType] {
  if parser.consume(#() then {
    var type = parser.parse_type_expr() or panic("expected type expr")
    parser.consume(#)) or panic("expected closing parenthesis")
    return some(type)
  }
  if parser.parse_symbol() is some(name) then
    return some(ToastType.call(ToastTypeCall {
      name, args = empty_slice[ToastType]()
    }))
  if parser.parse_name() is some(name) then
    return some(ToastType.variable(name))
  none[ToastType]()
}
fun parse_type_expr(parser: &Parser): Maybe[ToastType] {
  if parser.parse_symbol() is some(name) then {
    var args = list[ToastType]()
    loop args.&.push(parser.parse_type_atom() or break)
    return some(ToastType.call(ToastTypeCall { name, args = args.to_slice() }))
  }
  if parser.parse_type_atom() is some(type) then return some(type)
  none[ToastType]()
}

fun create_var(left: ToastExpr, right: ToastExpr): ToastVar {
  switch left
  case name(name)
    ToastVar { name, value = right.put_on_heap() }
  default panic("variables can only have a name on the left side")
}

fun to_toast_type(bacon: Bacon): ToastType {
  if bacon.line == "|" then {
    var variants = map[String, Slice[ToastType]]()
    for variant in bacon.children do {
      var parser = parser(variant.line)
      var name = parser.&.parse_symbol() or panic("expected variant name")
      var args =
        if variant.children.is_empty() then {
          var args = list[ToastType]()
          loop args.&.push(parser.&.parse_type_atom() or break)
          parser.&.is_done() or panic("additional garbage on line: {parser.input} cursor = {parser.cursor..parser.input.len}")
          args.to_slice()
        } else {
          parser.&.is_done() or panic("additional garbage on line: {parser.input} cursor = {parser.cursor..parser.input.len}")
          variant.children.to_toast_types()
        }
      variants.&.put(name, args)
    }
    ToastType.enum_(variants)
  } else {
    var parser = parser(bacon.line)
    if bacon.children.is_empty() then {
      var type = parser.&.parse_type_expr() or panic("expected type expr")
      parser.&.is_done() or panic("additional garbage on line: {parser.input} cursor = {parser.cursor..parser.input.len}")
      type
    } else {
      var name = parser.&.parse_symbol() or panic("expected type symbol (coz children)")
      parser.&.is_done() or panic("additional garbage on line: {parser.input} cursor = {parser.cursor..parser.input.len}")
      var args = bacon.children.to_toast_types()
      ToastType.call(ToastTypeCall { name, args })
    }
  }
}
fun to_toast_types(bacons: Slice[Bacon]): Slice[ToastType] {
  var types = list[ToastType]()
  for bacon in bacons do types.&.push(bacon.to_toast_type())
  types.to_slice()
}
fun to_toast_type_def(bacon: Bacon): Maybe[ToastTypeDef] {
  var parser = parser(bacon.line)
  var name = parser.&.parse_symbol() or return none[ToastTypeDef]()
  var params = list[String]()
  loop params.&.push(parser.&.parse_name() or break)
  parser.&.consume(#=) or panic("expected = after type signature")
  var type =
    if bacon.children.is_empty() then {
      var type = parser.&.parse_type_expr() or panic("expected type expr")
      parser.&.is_done() or panic("additional garbage on line: {parser.input} cursor = {parser.cursor..parser.input.len}")
      type
    } else {
      parser.&.is_done() or panic("additional garbage on line: {parser.input} cursor = {parser.cursor..parser.input.len}")
      bacon.children.len == 1 or panic("type def, but {bacon.children.len} types given")
      bacon.children.get(0).to_toast_type()
    }
  some(ToastTypeDef { name, params = params.to_slice(), type })
}

fun to_toast_expr(bacon: Bacon): ToastExpr {
  var parser = parser(bacon.line)
  var expr = parser.&.parse_expr() or panic("expected expression")
  var children = bacon.children.to_toast_exprs()
  if parser.&.consume(#=) then
    expr = ToastExpr.var_(create_var(expr, ToastExpr.body(children)))
  else if children.is_not_empty() then
    expr = ToastExpr.call(ToastCall {
      receiver = expr.put_on_heap(), args = children
    })
  parser.&.is_done() or panic("additional garbage on line: {parser.input} cursor = {parser.cursor..parser.input.len}, expr = {expr.debug()}")
  expr
}
fun parse_switch(bacon: Bacon, condition: ToastExpr): Maybe[ToastExpr] {
  bacon.line == "%" or return none[ToastExpr]()
  var cases = list[ToastCase]()
  for case_ in bacon.children do {
    var parser = parser(case_.line)
    var symbol = parser.&.parse_symbol() or panic("expected symbol of case")
    var params = list[String]()
    loop params.&.push(parser.&.parse_name() or break)
    parser.&.consume(#:) or panic("expected colon")
    var body =
      if parser.cursor == parser.input.len then {
        ToastExpr.body(case_.children.to_toast_exprs())
      } else {
        var expr = parser.&.parse_expr() or panic("expected case expression")
        parser.&.is_done() or panic("additional garbage on case line: {parser.input} cursor = {parser.cursor..parser.input.len}")
        case_.children.is_empty() or panic("case that is neither single- nor multi-line")
        expr
      }
    cases.&.push(ToastCase { symbol, params = params.to_slice(), body })
  }
  some(ToastExpr.switch_(ToastSwitch {
    condition = condition.put_on_heap(), cases = cases.to_slice()
  }))
}
fun parse_pipe(bacon: Bacon, previous: ToastExpr): Maybe[ToastExpr] {
  none[ToastExpr]()
}
fun to_toast_exprs(bacons: Slice[Bacon]): Slice[ToastExpr] {
  var exprs = list[ToastExpr]()
  var i = 0
  loop {
    var bacon = bacons.get_maybe(i) or break
    var expr = bacon.to_toast_expr()

    loop {
      var next = bacons.get_maybe(i + 1) or break
      if      next.parse_switch(expr) is some(new) then { expr = new }
      else if next.parse_pipe(expr)   is some(new) then { expr = new }
      else break
      i = i + 1
    }

    exprs.&.push(expr)
    i = i + 1
  }
  exprs.to_slice()
}
fun to_toast_fun_def(bacon: Bacon): Maybe[ToastFun] {
  var parser = parser(bacon.line)
  var name = parser.&.parse_name() or return none[ToastFun]()
  var params = list[ToastParam]()
  loop {
    var name = parser.&.parse_name() or break
    parser.&.consume(#:) or panic("expected : after param {name}")
    var type = parser.&.parse_type_atom() or panic("expected type of param {name}")
    params.&.push(ToastParam { name, type })
  }
  parser.&.consume("->") or panic("expected ->")
  var return_type = parser.&.parse_type_atom() or panic("expected return type")
  parser.&.consume(#=) or panic("expected =")

  var body =
    if bacon.children.is_empty() then {
      var body = parser.&.parse_expr() or panic("expected fun body")
      parser.&.is_done() or panic("additional garbage on line: {parser.input} cursor = {parser.cursor..parser.input.len}")
      body
    } else
      ToastExpr.body(bacon.children.to_toast_exprs())

  some(ToastFun { name, params = params.to_slice(), return_type, body })
}

fun to_toast(bacon: Bacon): Toast {
  bacon.line.is_empty() or panic("to_toast was given non-root Bacon")

  var defs = list[ToastDef]()
  for bacon in bacon.children do {
    if bacon.to_toast_type_def() is some(def) then {
      defs.&.push(ToastDef.type(def))
      continue
    }
    if bacon.to_toast_fun_def() is some(def) then {
      defs.&.push(ToastDef.fun_(def))
      continue
    }

    panic("couldn't parse line: {bacon.line}")
  }

  Toast { defs = defs.to_slice() }
}
