| Totally Okay Abstract Syntax Tree  
| Tree that represents the syntactical language constructs. This data structure
| should roughly correspond to the content of Plum files and be suitable for
| formatting etc.

import plum.mar

struct Toast { defs: Slice[ToastDef] }
enum ToastDef { type: ToastTypeDef, fun_: ToastFun }

struct ToastTypeDef { name: String, params: Slice[String], type: ToastType }
enum ToastType {
  variable: String,
  call: ToastTypeCall,
  enum_: Map[String, Slice[ToastType]],
}
struct ToastTypeCall { name: String, args: Slice[ToastType] }

struct ToastFun {
  name: String,
  params: Slice[ToastParam],
  return_type: ToastType,
  body: ToastExpr,
}
struct ToastParam { name: String, type: ToastType }
enum ToastExpr {
  int: Int,
  string: String,
  symbol: String,
  name: String,
  switch_: ToastSwitch,
  call: ToastCall,
  var_: ToastVar,
  body: Slice[ToastExpr],
}
struct ToastSwitch { condition: &ToastExpr, cases: Slice[ToastCase] }
struct ToastCase { symbol: String, params: Slice[String], body: ToastExpr }
struct ToastCall { receiver: &ToastExpr, args: Slice[ToastExpr] }
struct ToastVar { name: String, value: &ToastExpr }

fun write[W](writer: W, toast: Toast) {
  var first = true
  for def in toast.defs do {
    if first then first = false else writer."\n"
    switch def
    case type(def) {
      writer."{def.name}"
      for param in def.params do writer." {param}"
      writer." = "
      writer."{def.type}"
    }
    case fun_(def) {
      writer."{def.name}"
      for param in def.params do writer." {param.name}: {param.type}"
      writer." -> {def.return_type}"
      writer." =\n"
      writer.write(def.body, 1)
    }
  }
}
fun write[W](writer: W, type: ToastType) {
  switch type
  case variable(variable) writer."{variable}"
  case call(call) {
    writer."({call.name}"
    for arg in call.args do writer." {arg}"
    writer.")"
  }
  case enum_(variants) {
    writer."|"
    for variant in variants do {
      writer." ({variant.key}"
      for arg in variant.value do writer." {arg}"
      writer.")"
    }
  }
}
fun write[W](writer: W, body: Slice[ToastExpr], indentation: Int) {
  var first = true
  for expr in body do {
    if first then first = false else writer."\n"
    writer.write(expr, indentation)
  }
}
fun write[W](writer: W, expr: ToastExpr, indentation: Int) {
  if not(expr is body) then
    for i in 0..indentation do writer."  "
  switch expr
  case int(int) writer."{int}"
  case string(str) writer."\"{str}\""
  case symbol(symbol) writer."{symbol}"
  case name(name) writer."{name}"
  case switch_(switch_) {
    writer."%\n"
    writer.write(switch_.condition.*, indentation + 1)
    for case_ in switch_.cases do {
      writer."\n"
      for i in 0..indentation do writer."  "
      writer."  {case_.symbol}:\n"
      writer.write(case_.body, indentation + 2)
    }
  }
  case call(call) {
    writer."call\n"
    writer.write(call.receiver.*, indentation + 1)
    writer."\n"
    writer.write(call.args, indentation + 1)
  }
  case var_(var_) {
    writer."var {var_.name} =\n"
    writer.write(var_.value.*, indentation + 1)
  }
  case body(body) writer.write(body, indentation)
}

| In this file, parsing refers to converting a single line of code into
| something toasty.

struct Parser { input: String, input_src: Src, cursor: Int }
fun parser(input: String, src: Src): _ {
  Parser { input, input_src = src, cursor = 0 }
}
fun src_of_rest(parser: Parser): Src {
  parser.input_src.with_range(
    {parser.input_src.range.start + parser.cursor}..parser.input_src.range.end
  )
}
fun current(parser: Parser): Char {
  parser.input.chars().get_maybe(parser.cursor) or 0.lower_byte().to_char()
}
fun advance(parser: &Parser) { parser.cursor = parser.cursor + 1 }
fun is_at_end(parser: Parser): Bool { parser.cursor == parser.input.len }
fun consume_whitespace(parser: &Parser) {
  loop {
    parser.current().is_whitespace() or break
    parser.advance()
  }
}
fun is_done(parser: &Parser): Bool {
  parser.consume_whitespace()
  parser.is_at_end()
}
fun consume(parser: &Parser, char: Char): Maybe[Src] {
  parser.consume_whitespace()
  parser.current() == char or return none[Src]()
  var src = parser.input_src.with_range(
    {parser.input_src.range.start + parser.cursor} ..+ 1)
  parser.advance()
  some(src)
}
fun consume(parser: &Parser, prefix: String): Maybe[Src] {
  parser.consume_whitespace()
  var start = parser.cursor
  for char in prefix.chars() do {
    parser.current() == char or {
      parser.cursor = start
      return none[Src]()
    }
    parser.advance()
  }
  some(
    parser.input_src.with_range({parser.input_src.range.start + start} ..+ 1)
  )
}
fun parse_int(parser: &Parser): Maybe[Int] {
  parser.consume_whitespace()
  var num = 0
  var start = parser.cursor
  loop {
    var char = parser.current()
    char.is_digit() or break
    num = num * 10 + {char - #0}.to_int()
    parser.advance()
  }
  if start == parser.cursor then return none[Int]()
  some(num)
}

fun no_match[T](): Result[Maybe[T], Error] {
  ok[Maybe[T], Error](none[T]())
}
fun bad_input[T](error: Error): Result[Maybe[T], Error] {
  error[Maybe[T], Error](error)
}
fun parsed[T](value: T): Result[Maybe[T], Error] {
  ok[Maybe[T], Error](some(value))
}

fun parse_string(parser: &Parser): Result[Maybe[String], Error] {
  parser.consume(#") or return no_match[String]()
  var start = parser.cursor
  loop {
    if parser.is_at_end() then
      return bad_input[String](error(
        "string doesn't end until end of input", parser.src_of_rest()
      ))
    if parser.current() == #" then break
    parser.advance()
  }
  var end = parser.cursor
  parser.advance() | consume the "
  parsed(parser.input.substr(start..end))
}
var identifier_chars = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz_+-*/<>"
fun parse_identifier(parser: &Parser): Maybe[String] {
  parser.consume_whitespace()
  var start = parser.cursor
  loop {
    identifier_chars.iter().&.contains(parser.current()) or break
    parser.advance()
  }
  var end = parser.cursor
  if start == end then return none[String]()
  var identifier = parser.input.substr(start..end)
  if identifier == "->" then {
    parser.cursor = start
    return none[String]()
  }
  some(identifier)
}
fun parse_name(parser: &Parser): Maybe[String] {
  parser.consume_whitespace()
  not(parser.current().is_upper()) or return none[String]()
  parser.parse_identifier()
}
fun parse_symbol(parser: &Parser): Maybe[String] {
  parser.consume_whitespace()
  parser.current().is_upper() or return none[String]()
  parser.parse_identifier()
}
| Parses an expression that doesn't include whitespace (except in parentheses).
fun parse_atom(parser: &Parser): Result[Maybe[ToastExpr], Error] {
  if parser.consume(#() is some(paren_src) then {
    var expr = parser.parse_expr()? or
      return bad_input[ToastExpr](error(
        "Expected expression after parenthesis.", paren_src
      ))
    parser.consume(#)) or
      return bad_input[ToastExpr](error(
        "Expected the closing parenthesis.",
        parser.src_of_rest()
      ))
    return parsed(expr)
  }
  if parser.parse_int() is some(int) then return parsed(ToastExpr.int(int))
  if parser.parse_string()? is some(str) then return parsed(ToastExpr.string(str))
  if parser.parse_name() is some(name) then return parsed(ToastExpr.name(name))
  if parser.parse_symbol() is some(symbol) then return parsed(ToastExpr.symbol(symbol))
  no_match[ToastExpr]()
}
| Parses an expression that doesn't contain pipes (except in parentheses).
fun parse_unpiped_expr(parser: &Parser): Result[Maybe[ToastExpr], Error] {
  var expr = parser.parse_atom()? or return no_match[ToastExpr]()

  var args = list[ToastExpr]()
  loop args.&.push(parser.parse_atom()? or break)
  if args.is_not_empty() then
    expr = ToastExpr.call(ToastCall {
      receiver = expr.put_on_heap(), args = args.to_slice()
    })

  parsed(expr)
}
fun parse_expr(parser: &Parser): Result[Maybe[ToastExpr], Error] {
  var expr = parser.parse_unpiped_expr()? or return no_match[ToastExpr]()

  loop {
    parser.consume(#|) or break
    var piped_into = parser.parse_unpiped_expr()? or
      return bad_input[ToastExpr](error(
        "Expected expression", parser.src_of_rest()
      ))
    expr = piped_call(expr, piped_into)
  }
  if parser.consume(#=) is some(equals_src) then {
    var cursor_after_equals_sign = parser.cursor
    if parser.parse_expr()? is some(value) then
      expr = ToastExpr.var_(create_var(expr, value, equals_src)?)
    else
      parser.cursor = cursor_after_equals_sign - 1
  }

  parsed(expr)
}
fun parse_type_atom(parser: &Parser): Result[Maybe[ToastType], Error] {
  if parser.consume(#() is some(paren_src) then {
    var type = parser.parse_type_expr()? or
      return bad_input[ToastType](error(
        "Expected expression after parenthesis.", paren_src
      ))
    parser.consume(#)) or
      return bad_input[ToastType](error(
        "Expected a closing parenthesis.", parser.src_of_rest()
      ))
    return parsed(type)
  }
  if parser.parse_symbol() is some(name) then
    return parsed(ToastType.call(ToastTypeCall {
      name, args = empty_slice[ToastType]()
    }))
  if parser.parse_name() is some(name) then
    return parsed(ToastType.variable(name))
  no_match[ToastType]()
}
fun parse_type_expr(parser: &Parser): Result[Maybe[ToastType], Error] {
  if parser.parse_symbol() is some(name) then {
    var args = list[ToastType]()
    loop args.&.push(parser.parse_type_atom()? or break)
    return parsed(ToastType.call(ToastTypeCall { name, args = args.to_slice() }))
  }
  if parser.parse_type_atom()? is some(type) then return parsed(type)
  no_match[ToastType]()
}

fun create_var(
  left: ToastExpr, right: ToastExpr, equals_src: Src
): Result[ToastVar, Error] {
  switch left
  case name(name)
    ok[ToastVar, Error](ToastVar { name, value = right.put_on_heap() })
  default
    error[ToastVar, Error](error(
      "variables can only have a name on the left side", equals_src
    ))
}

fun to_toast_type(bacon: Bacon): Result[ToastType, Error] {
  if bacon.line == "|" then {
    var variants = map[String, Slice[ToastType]]()
    for variant in bacon.children do {
      var parser = parser(variant.line, variant.src)
      var name = parser.&.parse_symbol() or
        return error[ToastType, Error](error(
          "Expected variant name.", parser.src_of_rest()
        ))
      var args =
        if variant.children.is_empty() then {
          var args = list[ToastType]()
          loop args.&.push(parser.&.parse_type_atom()? or break)
          parser.&.is_done() or
            return error[ToastType, Error](error(
              "Additional garbage on line.", parser.src_of_rest()
            ))
          args.to_slice()
        } else {
          parser.&.is_done() or
            return error[ToastType, Error](error(
              "Additional garbage on line.", parser.src_of_rest()
            ))
          variant.children.to_toast_types()?
        }
      variants.&.put(name, args)
    }
    ok[ToastType, Error](ToastType.enum_(variants))
  } else {
    var parser = parser(bacon.line, bacon.src)
    if bacon.children.is_empty() then {
      var type = parser.&.parse_type_expr()? or
        return error[ToastType, Error](error(
          "expected type expr", parser.src_of_rest()
        ))
      parser.&.is_done() or
        return error[ToastType, Error](error(
          "Additional garbage on line", parser.src_of_rest()
        ))
      ok[ToastType, Error](type)
    } else {
      var name = parser.&.parse_symbol() or
        return error[ToastType, Error](error(
          "Expected type symbol (coz children)", parser.src_of_rest()
        ))
      parser.&.is_done() or
        return error[ToastType, Error](error(
          "Additional garbage on line", parser.src_of_rest()
        ))
      var args = bacon.children.to_toast_types()?
      ok[ToastType, Error](ToastType.call(ToastTypeCall { name, args }))
    }
  }
}
fun to_toast_types(bacons: Slice[Bacon]): Result[Slice[ToastType], Error] {
  var types = list[ToastType]()
  for bacon in bacons do types.&.push(bacon.to_toast_type()?)
  ok[Slice[ToastType], Error](types.to_slice())
}
fun to_toast_type_def(bacon: Bacon): Result[Maybe[ToastTypeDef], Error] {
  var parser = parser(bacon.line, bacon.src)
  var name = parser.&.parse_symbol() or return no_match[ToastTypeDef]()
  var params = list[String]()
  loop params.&.push(parser.&.parse_name() or break)
  parser.&.consume(#=) or
    return bad_input[ToastTypeDef](error(
      "expected = after type signature", parser.src_of_rest()
    ))
  var type =
    if bacon.children.is_empty() then {
      var type = parser.&.parse_type_expr()? or
        return bad_input[ToastTypeDef](error(
          "Expected type expression.", parser.src_of_rest()
        ))
      parser.&.is_done() or
        return bad_input[ToastTypeDef](error(
          "Additional garbage on line.", parser.src_of_rest()
        ))
      type
    } else {
      parser.&.is_done() or
        return bad_input[ToastTypeDef](error(
          "Additional garbage on line.", parser.src_of_rest()
        ))
      bacon.children.len == 1 or
        return bad_input[ToastTypeDef](error(
          "Type definition, but it has {bacon.children.len} children.",
          bacon.src.with_range(
            bacon.children.first().src.range.start
              ..bacon.children.last().src.range.end
          ),
        ))
      bacon.children.get(0).to_toast_type()?
    }
  parsed(ToastTypeDef { name, params = params.to_slice(), type })
}

fun to_toast_expr(bacon: Bacon): Result[Maybe[ToastExpr], Error] {
  var parser = parser(bacon.line, bacon.src)
  if parser.&.consume(##) is some then return no_match[ToastExpr]()
  var expr = parser.&.parse_expr()? or
    return bad_input[ToastExpr](error(
      "Expected expression", parser.src_of_rest()
    ))
  var children = bacon.children.to_toast_exprs()?
  if parser.&.consume(#=) is some(equals_src) then
    expr = ToastExpr.var_(
      create_var(expr, ToastExpr.body(children), equals_src)?
    )
  else if children.is_not_empty() then
    expr = ToastExpr.call(ToastCall {
      receiver = expr.put_on_heap(), args = children
    })
  parser.&.is_done() or
    return bad_input[ToastExpr](error(
      "Additional garbage on line.", parser.src_of_rest()
    ))
  parsed(expr)
}
fun parse_switch(
  bacon: Bacon, condition: ToastExpr
): Result[Maybe[ToastExpr], Error] {
  bacon.line == "%" or return no_match[ToastExpr]()
  var cases = list[ToastCase]()
  for case_ in bacon.children do {
    var parser = parser(case_.line, case_.src)
    var symbol = parser.&.parse_symbol() or
      return bad_input[ToastExpr](error(
        "Expected the symbol of a case.", parser.src_of_rest()
      ))
    var params = list[String]()
    loop params.&.push(parser.&.parse_name() or break)
    parser.&.consume(#:) or
      return bad_input[ToastExpr](error(
        "Expected a colon.", parser.src_of_rest()
      ))
    var body =
      if parser.cursor == parser.input.len then {
        ToastExpr.body(case_.children.to_toast_exprs()?)
      } else {
        var expr = parser.&.parse_expr()? or
          return bad_input[ToastExpr](error(
            "Expected a case expression.", parser.src_of_rest()
          ))
        parser.&.is_done() or
          return bad_input[ToastExpr](error(
            "There's additional garbage on a case line.", parser.src_of_rest()
          ))
        case_.children.is_empty() or
          return bad_input[ToastExpr](error(
            "A case is neither single- nor multi-line.", parser.src_of_rest()
          ))
        expr
      }
    cases.&.push(ToastCase { symbol, params = params.to_slice(), body })
  }
  parsed(ToastExpr.switch_(ToastSwitch {
    condition = condition.put_on_heap(), cases = cases.to_slice()
  }))
}
fun parse_pipe(
  bacon: Bacon, previous: ToastExpr
): Result[Maybe[ToastExpr], Error] {
  bacon.line == "|" or return no_match[ToastExpr]()
  var children = bacon.children.to_toast_exprs()?
  children.len == 1 or
    return bad_input[ToastExpr](error(
      "Pipe with multiple children",
      bacon.src.with_range(bacon.src.range.start..{bacon.src.range.start + 1})
    ))
  var piped_into = children.get(0)
  parsed(piped_call(previous, piped_into))
}
fun piped_call(left: ToastExpr, piped_into: ToastExpr): ToastExpr {
  if piped_into is call(call) then {
    var args = list(left)
    for arg in call.args do args.&.push(arg)
    ToastExpr.call(ToastCall {
      receiver = call.receiver, args = args.to_slice()
    })
  } else
    ToastExpr.call(ToastCall {
      receiver = piped_into.put_on_heap(), args = list(left).to_slice()
    })
}
fun to_toast_exprs(bacons: Slice[Bacon]): Result[Slice[ToastExpr], Error] {
  var exprs = list[ToastExpr]()
  var i = 0
  loop {
    var bacon = bacons.get_maybe(i) or break
    var expr = bacon.to_toast_expr()? or { i = i + 1  continue }

    loop {
      var next = bacons.get_maybe(i + 1) or break
      if      next.parse_switch(expr)? is some(new) then { expr = new }
      else if next.parse_pipe(expr)?   is some(new) then { expr = new }
      else break
      i = i + 1
    }

    exprs.&.push(expr)
    i = i + 1
  }
  ok[Slice[ToastExpr], Error](exprs.to_slice())
}
fun to_toast_fun_def(bacon: Bacon): Result[Maybe[ToastFun], Error] {
  var parser = parser(bacon.line, bacon.src)
  var name = parser.&.parse_name() or return no_match[ToastFun]()
  var params = list[ToastParam]()
  loop {
    var name = parser.&.parse_name() or break
    parser.&.consume(#:) or
      return bad_input[ToastFun](error(
        "Expected : after param {name}.", parser.src_of_rest()
      ))
    var type = parser.&.parse_type_atom()? or
      return bad_input[ToastFun](error(
        "Expected type of param {name}", parser.src_of_rest()
      ))
    params.&.push(ToastParam { name, type })
  }
  parser.&.consume("->") or
    return bad_input[ToastFun](error(
      "Expected ->", parser.src_of_rest()
    ))
  var return_type = parser.&.parse_type_atom()? or
    return bad_input[ToastFun](error(
      "expected return type",
      parser.src_of_rest()
    ))
  parser.&.consume(#=) or
    return bad_input[ToastFun](error(
      "expected =", parser.src_of_rest()
    ))

  var body =
    if bacon.children.is_empty() then {
      var body = parser.&.parse_expr()? or
        return bad_input[ToastFun](error(
          "Expected fun body.", parser.src_of_rest()
        ))
      parser.&.is_done() or
        return bad_input[ToastFun](error(
          "Additional garbage on line.", parser.src_of_rest()
        ))
      body
    } else
      ToastExpr.body(bacon.children.to_toast_exprs()?)

  parsed(ToastFun { name, params = params.to_slice(), return_type, body })
}

fun to_toast(bacon: Bacon): Result[Toast, Error] {
  bacon.line.is_empty() or panic("to_toast was given non-root Bacon")

  var defs = list[ToastDef]()
  for bacon in bacon.children do {
    if bacon.to_toast_type_def()? is some(def) then {
      defs.&.push(ToastDef.type(def))
      continue
    }
    if bacon.to_toast_fun_def()? is some(def) then {
      defs.&.push(ToastDef.fun_(def))
      continue
    }
    if bacon.line.starts_with("#") then continue

    return error[Toast, Error](error("Couldn't parse line.", bacon.src))
  }

  ok[Toast, Error](Toast { defs = defs.to_slice() })
}
